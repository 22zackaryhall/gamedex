buildscript {
    ext {
        appVersion = '0.1.0'
        javaMaxHeap = '3g'

        ktlin = [:].with {
            v = '1.3.11'
            majorV = '1.3'
            std = [group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib-jdk8', version: v]
            reflect = [group: 'org.jetbrains.kotlin', name: 'kotlin-reflect', version: v]
            delegate
        }

        coroutines = [:].with {
            v = '1.1.0'
            core = [group: 'org.jetbrains.kotlinx', name: 'kotlinx-coroutines-core', version: v]
            javaFx = [group: 'org.jetbrains.kotlinx', name: 'kotlinx-coroutines-javafx', version: v]
            debug = [group: 'org.jetbrains.kotlinx', name: 'kotlinx-coroutines-debug', version: v]
            test = [group: 'org.jetbrains.kotlinx', name: 'kotlinx-coroutines-test', version: v]
            delegate
        }

        guice = [group: 'com.google.inject', name: 'guice', version: '4.2.2']
        reflections = [group: 'org.reflections', name: 'reflections', version: '0.9.11']

        slf4j = [:].with {
            v = '1.7.25'
            api = [group: 'org.slf4j', name: 'slf4j-api', version: v]
            julBridge = [group: 'org.slf4j', name: 'jul-to-slf4j', version: v]
            delegate
        }
        logback = [group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3']

        semVer = 'net.swiftzer.semver:semver:1.1.1'
        
        typesafeConfig = [group: 'com.typesafe', name: 'config', version: '1.3.3']
        config4k = [group: 'io.github.config4k', name: 'config4k', version: '0.4.1']

        guava = [group: 'com.google.guava', name: 'guava', version: '27.0.1-jre']

        jodaTime = [group: 'joda-time', name: 'joda-time', version: '2.10.1']

        khttp = [group: 'com.github.jkcclemens', name: 'khttp', version: 'master-SNAPSHOT']

        jackson = [:].with {
            v = '2.9.8'
            core = [group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: v]
            kotlin = [group: 'com.fasterxml.jackson.module', name: 'jackson-module-kotlin', version: v]
            joda = [group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-joda', version: v]
            delegate
        }

        h2 = [group: 'com.h2database', name: 'h2', version: '1.4.196']
        exposed = [group: 'org.jetbrains.exposed', name: 'exposed', version: '0.11.2']

        controlsFx = [group: 'org.controlsfx', name: 'controlsfx', version: '8.40.14']
        jFoenix = [:].with {
            core = [group: 'com.jfoenix', name: 'jfoenix', version: '8.0.8']
            delegate
        }
        tornadoFx = [:].with {
            core = [group: 'no.tornado', name: 'tornadofx', version: '1.7.18']
            controls = [group: 'no.tornado', name: 'tornadofx-controls', version: '1.0.6']
            delegate
        }
        ikonli = [:].with {
            v = '2.4.0'
            javaFx = [group: 'org.kordamp.ikonli', name: 'ikonli-javafx', version: v]
            fontAwesome = [group: 'org.kordamp.ikonli', name: 'ikonli-fontawesome5-pack', version: v]
            materialDesign = [group: 'org.kordamp.ikonli', name: 'ikonli-materialdesign-pack', version: v]
            delegate
        }
//        bootstrapFx = [group: 'org.kordamp.bootstrapfx', name: 'bootstrapfx-core', version: '0.2.2']

        diff = [group: 'com.googlecode.java-diff-utils', name: 'diffutils', version: '1.3.0']
        completely = [group: 'com.miguelfonseca.completely', name: 'completely-core', version: '0.8.0']

        kotlinTest = [group: 'io.kotlintest', name: 'kotlintest', version: '2.0.7']
        mockK = [group: 'io.mockk', name: 'mockk', version: '1.9']
        junit = [group: 'junit', name: 'junit', version: '4.12']
        mockito = [group: 'org.mockito', name: 'mockito-all', version: '1.10.19']

        ktor = [:].with {
            v = '1.1.1'
            core = [group: 'io.ktor', name: 'ktor-server-core', version: v]
            netty = [group: 'io.ktor', name: 'ktor-server-netty', version: v]
            clientCore = [group: 'io.ktor', name: 'ktor-client-core-jvm', version: v]
//            clientLogging = [group: 'io.ktor', name: 'ktor-client-logging-jvm', version: v]
            clientApache = [group: 'io.ktor', name: 'ktor-client-apache', version: v]
            clientJackson = [group: 'io.ktor', name: 'ktor-client-jackson', version: v]
            delegate
        }
        wiremock = [group: 'com.github.tomakehurst', name: 'wiremock', version: '2.20.0']
    }

    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
        maven { url "https://plugins.gradle.org/m2/" }
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:${ktlin.v}"
        classpath "de.dynamicfiles.projects.gradle.plugins:javafx-gradle-plugin:8.8.2"
    }
}

plugins {
    id 'com.github.hierynomus.license' version '0.15.0'
    id 'com.github.ben-manes.versions' version '0.20.0'
    id 'com.peterabeles.gversion' version '1.4.1'
    id 'net.vivin.gradle-semantic-build-versioning' version '4.0.0' apply false
}

// Equivalent to calling --continue from the command line.
// Will run all tests and produce a report of all failed tests at the end.
gradle.startParameter.continueOnFailure = true

apply gradleFile('versions.gradle')

gversion {
    srcDir = rootProject.rootDir.relativePath(project(":core:gamedex-core-common").file("src/main/kotlin/")).normalize()
    classPackage = "com.gitlab.ykrasik.gamedex.core.version"
    className = "RawVersion"
    language = "kotlin"
}

tag {
    message {
        "version: ${version}"
    }
}

allprojects {
    apply plugin: 'java'
    apply plugin: 'maven-publish'

    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    group 'com.gitlab.ykrasik'

    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
        maven { url "https://jitpack.io" }
        maven { url "http://dl.bintray.com/kotlin/exposed" }
        maven { url "http://dl.bintray.com/kotlin/ktor" }
        maven { url "https://kotlin.bintray.com/kotlinx" }
    }
}

subprojects {
    apply plugin: 'kotlin'
    apply gradleFile('test.gradle')
    apply gradleFile('license.gradle')
    version = rootProject.version

    dependencies {
        compile ktlin.std
        compile ktlin.reflect
        compile coroutines.core

        compile slf4j.api

        compile guice

        testCompile kotlinTest
        testCompile mockK

        testCompile coroutines.debug
        testCompile coroutines.test
    }

    compileKotlin {
        kotlinOptions.jvmTarget = '1.8'
        kotlinOptions.apiVersion = ktlin.majorV
        kotlinOptions.languageVersion = ktlin.majorV
    }
    compileTestKotlin {
        kotlinOptions.jvmTarget = '1.8'
        kotlinOptions.apiVersion = ktlin.majorV
        kotlinOptions.languageVersion = ktlin.majorV
    }
    kotlin {
    }
}

project("gamedex-api") {
    dependencies {
        compile typesafeConfig
        compile config4k

        compile guava

        compile jodaTime

        compile ktor.clientCore
        testCompile ktor.clientApache

        compile jackson.core
        compile jackson.kotlin
        compile jackson.joda

        testCompile wiremock
        testCompile ktor.core
        testCompile ktor.netty
        testCompile reflections
        testCompile logback
    }

    task sourcesJar(type: Jar, dependsOn: classes) {
        classifier 'sources'
        from sourceSets.main.allSource
    }

    task testSourcesJar(type: Jar, dependsOn: classes) {
        appendix 'testkit'
        classifier 'sources'
        from sourceSets.test.allSource
        exclude '**/*.jpg'
        exclude '**/words.txt'
        exclude '**/genres.txt'
    }

    publishing {
        publications {
            main(MavenPublication) {
                from components.java
                artifact sourcesJar
            }
            test(MavenPublication) {
                artifactId 'gamedex-api-testkit'
                artifact testJar
                artifact testSourcesJar

                pom.withXml {
                    def dependencies = asNode().appendNode('dependencies')
                    configurations.testArtifacts.getResolvedConfiguration().getFirstLevelModuleDependencies().each {
                        def dependency = dependencies.appendNode('dependency')
                        dependency.appendNode('groupId', it.moduleGroup)
                        dependency.appendNode('artifactId', it.moduleName)
                        dependency.appendNode('version', it.moduleVersion)
                    }
                }
            }
        }
    }
}

project("app:gamedex-app-api") {
    dependencies {
        fullDependency ":gamedex-api"
        compile diff
    }
}

project("core:gamedex-core-common") {
    dependencies {
        fullDependency ":app:gamedex-app-api"
        
        compile semVer
    }

    compileKotlin.dependsOn(createVersionFile)
}

project("core:gamedex-core-file") {
    dependencies {
        fullDependency ":core:gamedex-core-common"
    }
}

project("core:gamedex-core-game") {
    dependencies {
        fullDependency ":core:gamedex-core-common"
        fullDependency ":core:gamedex-core-persistence"

        compile completely
    }
}

project("core:gamedex-core-maintenance") {
    dependencies {
        fullDependency ":core:gamedex-core-common"
        fullDependency ":core:gamedex-core-persistence"
    }
}

project("core:gamedex-core-image") {
    dependencies {
        fullDependency ":core:gamedex-core-common"
    }
}

project("core:gamedex-core-library") {
    dependencies {
        fullDependency ":core:gamedex-core-common"
        fullDependency ":core:gamedex-core-persistence"
    }
}

project("core:gamedex-core-log") {
    dependencies {
        fullDependency ":core:gamedex-core-common"

        compile logback
        compile slf4j.julBridge
    }
}

project("core:gamedex-core-persistence") {
    dependencies {
        fullDependency ":gamedex-api"
        compile h2
        compile exposed
    }
}


project("core:gamedex-core-provider") {
    dependencies {
        fullDependency ":core:gamedex-core-common"
    }
}

project("core:gamedex-core-report") {
    dependencies {
        fullDependency ":core:gamedex-core-common"
    }
}

project("core:gamedex-core") {
    dependencies {
        fullDependency ":core:gamedex-core-common"
        fullDependency ":core:gamedex-core-file"
        fullDependency ":core:gamedex-core-image"
        fullDependency ":core:gamedex-core-game"
        fullDependency ":core:gamedex-core-maintenance"
        fullDependency ":core:gamedex-core-library"
        fullDependency ":core:gamedex-core-log"
        fullDependency ":core:gamedex-core-persistence"
        fullDependency ":core:gamedex-core-provider"
        fullDependency ":core:gamedex-core-report"

        compile ktor.clientApache
        compile reflections
    }
}

project("app:javafx:ikonli-material-design-pack") {
    dependencies {
        compile ikonli.javaFx
    }
}

project("app:javafx:gamedex-javafx-common") {
    dependencies {
        fullDependency ":app:gamedex-app-api"
        compile project(":app:javafx:ikonli-material-design-pack")

        compile tornadoFx.core
        compile tornadoFx.controls

        compile jFoenix.core

        compile controlsFx

        compile coroutines.javaFx
    }
}

project("app:javafx:gamedex-javafx-view") {
    dependencies {
        fullDependency ":app:javafx:gamedex-javafx-common"
    }
}

project("provider") {
    subprojects.each {
        it.fullDependency ":gamedex-api"
    }
}

def gradleFile(String fileName) { [from: file("gradle/$fileName")] }