buildscript {
    repositories {
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }
    dependencies {
        classpath 'gradle.plugin.net.vivin:gradle-semantic-build-versioning:4.0.0'
    }
}

rootProject.name = 'gamedex'
apply plugin: 'net.vivin.gradle-semantic-build-versioning'

/******************************* Projects Start *******************************/
include "gamedex-api"
includeSubDirectories("core")

include "app:gamedex-app-api"
includeSubDirectories("app/javafx")

includeSubDirectories("provider")
/******************************** Projects End ********************************/

setDefaultBuildFile(rootProject)

def includeSubDirectories(String path) {
    new File(rootDir, path).eachDir { dir ->
        if (dir.name != 'build') {
            include "${path.replace('/', ':')}:${dir.name}"
        }
    }
}

def setDefaultBuildFile(def project) {
    project.children.each { child ->
        child.buildFileName = "${child.name}.gradle"
        setDefaultBuildFile(child)
    }
}